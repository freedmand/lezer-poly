// The Poly grammar specification in Lezer
//
// Note: special documentation comments are used to generate AST
// forming code. See ../grammarConvert/grammarConvert.ts for details.

@precedence { unary @right, times @left, plus @left, comparison @left, equals @left, union @left, closure @right, dictionary @cut }

@top Program { /** Statement[] */ statement* }

// Statements
statement[@isGroup=Statement] {
  /** | */
  DeclareStatement |
  TypeDeclareStatement |
  DeclareFunctionStatement |
  DeclareTypeFunctionStatement |
  AssignStatement |
  PlusAssignStatement |
  MinusAssignStatement |
  TimesAssignStatement |
  DivideAssignStatement |
  BlockStatement |
  IfStatement |
  ForStatement |
  ExpressionStatement |
  FunctionStatement |
  ReturnStatement
}

DeclareStatement {
  /** Declarator Identifier Expression? = Expression */
  makeStatement<Declarator Identifier optionalType "=" Expression>
}

TypeDeclareStatement {
  /** type TypeIdentifier Expression? = Expression */
  makeStatement<keyword<"type"> TypeIdentifier optionalType "=" Expression>
}

DeclareFunctionStatement {
  /** Declarator Identifier ( Param[] ) Expression? ClosureOrBlockStatement */
  Declarator Identifier "(" params ")" optionalReturnType ClosureOrBlockStatement
}

DeclareTypeFunctionStatement {
  /** type TypeIdentifier ( Param[] ) Expression? ClosureOrBlockStatement */
  keyword<"type"> TypeIdentifier "(" params ")" optionalReturnType ClosureOrBlockStatement
}

Declarator {
  /** | */
  LetDeclarator | VarDeclarator
}

LetDeclarator {
  /** let */
  keyword<"let">
}

VarDeclarator {
  /** var */
  keyword<"var">
}

AssignStatement {
  /** Identifier = Expression */
  makeAssignStatement<"=">
}

PlusAssignStatement {
  /** Identifier += Expression */
  makeAssignStatement<"+=">
}

MinusAssignStatement {
  /** Identifier -= Expression */
  makeAssignStatement<"-=">
}

TimesAssignStatement {
  /** Identifier *= Expression */
  makeAssignStatement<"*=">
}

DivideAssignStatement {
  /** Identifier /= Expression */
  makeAssignStatement<"/=">
}

BlockStatement {
  /** { Statement[] } */
  "{" statement* "}"
}

ClosureOrBlockStatement {
  /** | */
  ArrowExpressionStatement | BlockStatement
}

ClosureOrBlockExpression {
  /** | */
  ArrowExpression | BlockStatement
}

FunctionStatement {
  /** fn Identifier ( Param[] ) Expression? ClosureOrBlockStatement */
  keyword<"fn"> Identifier "(" params ")" optionalReturnType ClosureOrBlockStatement
}

ClosureExpression {
  /** ( Param[] ) Expression? ClosureOrBlockExpression */
  "(" params ")" optionalReturnType ClosureOrBlockExpression
}

ReturnStatement {
  /** return Expression? */
  makeStatement<keyword<"return"> Expression?>
}

IfStatement {
  /** if Expression BlockStatement */
  keyword<"if"> Expression BlockStatement
}

ForStatement {
  /** for Expression in Expression BlockStatement */
  keyword<"for"> Expression keyword<"in"> Expression BlockStatement
}

ExpressionStatement {
  /** Expression */
  makeStatement<Expression>
}

ArrowExpressionStatement {
  /** => Expression */
  makeStatement<"=>" Expression>
}

ArrowExpression {
  /** => Expression */
  !closure "=>" Expression
}

// Expressions
Expression {
  /** | */
  Identifier | Number | String | BinaryExpression | GroupExpression |
  Negate | MemberExpression | CallExpression | ListExpression | DictionaryExpression |
  ClosureExpression
}

ListExpression {
  /** [ Expression[] ] */
  "[" commaSep<Expression> "]"
}

DictionaryExpression {
  /** { DictionaryItem[] } */
  !dictionary "{" commaSep<DictionaryItem> "}"
}

DictionaryItem {
  /** Identifier :? Expression? */
  Identifier (":" Expression)?
}

MemberExpression {
  /** Expression . Identifier */
  Expression "." Identifier
}

CallExpression {
  /** Expression ( Arg[] ) */
  Expression "(" args ")"
}

GroupExpression {
  /** ( Expression ) */
  "(" Expression ")"
}

BinaryExpression {
  /** | */
  PlusExpression | MinusExpression | TimesExpression | DivideExpression |
  EqualityExpression | InequalityExpression | GreaterThanExpression | LessThanExpression | GreaterThanOrEqualExpression | LessThanOrEqualExpression |
  UnionExpression
}

PlusExpression {
  /** Expression + Expression */
  Expression !plus "+" Expression
}

MinusExpression {
  /** Expression - Expression */
  Expression !plus "-" Expression
}

TimesExpression {
  /** Expression * Expression */
  Expression !times "*" Expression
}

DivideExpression {
  /** Expression / Expression */
  Expression !times Divide Expression
}

EqualityExpression {
  /** Expression == Expression */
  Expression !equals "==" Expression
}

InequalityExpression {
  /** Expression != Expression */
  Expression !equals "!=" Expression
}

GreaterThanExpression {
  /** Expression > Expression */
  Expression !comparison ">" Expression
}

LessThanExpression {
  /** Expression < Expression */
  Expression !comparison "<" Expression
}

GreaterThanOrEqualExpression {
  /** Expression >= Expression */
  Expression !comparison ">=" Expression
}

LessThanOrEqualExpression {
  /** Expression <= Expression */
  Expression !comparison "<=" Expression
}

UnionExpression {
  /** Expression | Expression */
  Expression !union "|" Expression
}

Negate {
  /** - Expression */
  !unary "-" Expression
}

// Miscellaneous
Param {
  /** | */
  TypeParam | ValueParam
}

TypeParam {
  /** type TypeIdentifier */
  keyword<"type"> TypeIdentifier
}

ValueParam {
  /** ParamIdentifier Expression? Expression? */
  ParamIdentifier optionalType optionalDefault
}

ParamIdentifier {
  /** | */
  ParamValueIdentifierToken | ParamTypeIdentifierToken
}

ParamValueIdentifierToken {
  /** ValueIdentifier */
  ValueIdentifier ~closure
}

ParamTypeIdentifierToken {
  /** TypeIdentifier */
  TypeIdentifier ~closure
}

Arg {
  /** Identifier? =? Expression */
  (Identifier "=")? Expression
}

Identifier {
  /** | */
  ValueIdentifierToken | TypeIdentifierToken
}

ValueIdentifierToken {
  /** ValueIdentifier */
  ValueIdentifier ~closure
}

TypeIdentifierToken {
  /** TypeIdentifier */
  TypeIdentifier ~closure
}

optionalType {
  (":" Expression)?
}

optionalReturnType {
  ("->" Expression)?
}

optionalDefault {
  ("=" Expression)?
}

params {
  commaSep<Param>
}

args {
  commaSep<Arg>
}

// Skip / strings
@skip {
  whitespace | Comment | DocComment
}

@skip {} {
  String {
    /** " StringChunk[] " */
    '"' StringChunk* '"'
  }
}

StringChunk {
  /** | */
  StringContent | Template
}

StringContent {
  /** . */
  char
}

Template {
  /** \[ Expression ] */
  "\\[" Expression "]"
}

// Utils
makeStatement<content> { content ";" }
makeAssignStatement<operator> { makeStatement<Identifier operator Expression> }
commaSep1<item> { item ("," item)* }
commaSep<item> { commaSep1<item>? }
keyword<content> { @specialize[@name={content}]<ValueIdentifier, content> }

@tokens {
  ValueIdentifier { /** . */ "@"? @asciiLowercase @asciiLetter* }
  TypeIdentifier { /** . */ @asciiUppercase @asciiLetter* }
  Number { /** . */ @digit+ }
  Divide { "/" }
  Comment {
    "//" ![\n]*
  }
  DocComment {
    "///" ![\n]*
  }
  whitespace { @whitespace+ }
  char { ($[\u{20}\u{21}\u{23}-\u{5b}\u{5d}-\u{10ffff}] | "\\" esc)+ }
  esc  { $["\\\/bfnrt] | "u" hex hex hex hex }
  hex  { $[0-9a-fA-F] }
  "+" "-" "*" "/" "[" "]" "{" "}" "\\["
  @precedence { DocComment, Comment, Divide }
}

@external propSource polyHighlighting from "../grammar/propSource.js"
